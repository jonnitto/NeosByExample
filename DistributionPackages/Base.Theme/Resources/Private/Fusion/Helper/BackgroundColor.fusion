prototype(Base.Theme:Helper.BackgroundColor) < prototype(Neos.Fusion:Component) {
    @propTypes {
        backgroundColor = ${PropTypes.oneOf(['neos-dark', 'neos-light', 'slate-50', ''])}
        dark = ${PropTypes.boolean}
    }
    backgroundColor = ${value}
    dark = null

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}
        _class = Neos.Fusion:Match {
            @subject = ${props.backgroundColor}
            @default = 'bg-slate-50'
            neos-dark = 'bg-neos-dark'
            neos-light = 'bg-neos-light'
        }

        renderer = Neos.Fusion:DataStructure {
            hasBackgroud = ${!!props.backgroundColor}
            onNeosBg = ${String.startsWith(props.backgroundColor, 'neos')}
            dark = ${Type.isBoolean(props.dark) ? props.dark : props.backgroundColor == 'neos-dark'}
            class = ${Carbon.String.merge(props._class, this.dark ? 'text-slate-100 -links-light' : ('text-slate-900' + (props._class == 'bg-slate-50' ? '' : ' -links-dark')))}
        }
    }
}
